plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.thxno'
version = '1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.junit.platform:junit-platform-runner:1.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.vintage:junit-vintage-engine'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.23.0'
    testImplementation 'org.mockito:mockito-inline:3.8.0'
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    api 'com.alibaba:fastjson:1.2.73'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:30.0-jre'

    implementation 'org.jsoup:jsoup:1.14.1'

    implementation 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
}

tasks.named('test') {
    // Use junit platform for unit tests.
    useJUnitPlatform()
}

jar {
    from {
        configurations
                .runtimeClasspath
                .collect {
                    zipTree(it)
                }
    }
    duplicatesStrategy(DuplicatesStrategy.WARN)
    from sourceSets.main.output
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'fusheng-runner'
            from components.java
//            artifact jarWithSources
            pom {
                name = 'fusheng runner'
                description = 'fusheng livedoc core lib'
                url = 'https://github.com/ThxNo/fusheng-runner'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = developerName
                        email = developerEmail
                    }
                }
                scm {
                    url = 'https://github.com/ThxNo/fusheng-runner'
                    connection = 'scm:git:git://github.com/ThxNo/fusheng-runner.git'
                    developerConnection = 'scm:git:ssh://git@github.com/ThxNo/fusheng-runner.git'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonaqubeUsername
                password sonaqubePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    // <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
    // 防止本地打开中文乱码
    options.addStringOption("charset", "UTF-8")
}